;;; -*- Mode:Lisp; Syntax:ANSI-Common-Lisp; Coding:utf-8 -*-

(in-package #:cl-random-tests)

(defsuite continuous-time-tests (tests))

(deftest uniformized-markov-jump (continuous-time-tests)
  (let* ((n 100000)
         (keys #(:a :b :c))
         (rates #(1 2 3))
         (rv (r-uniformized-markov-jump rates :transition-rate 10 :keys keys
                                              :no-change :no-change))
         (possible-keys (concatenate 'vector (vector :no-change) keys))
         (durations (make-array n :element-type 'double-float))
         (jumps (make-array n)))
    (dotimes (i n)
      (setf (values (aref durations i) (aref jumps i)) (draw rv)))
    (let ((*num=-tolerance* 0.01d0))
      (assert-equality #'num= 0.1 (clnu:mean durations)))
    (let ((*num=-tolerance* 0.1d0))
      (assert-equality #'num= (* 0.1 n) (count :a jumps))
      (assert-equality #'num= (* 0.2 n) (count :b jumps))
      (assert-equality #'num= (* 0.3 n) (count :c jumps))
      (assert-equality #'num= (* 0.4 n) (count :no-change jumps)))
    (assert-true (every #'alexandria:non-negative-real-p durations))
    (assert-true (every (lambda (j) (find j possible-keys)) jumps))))

(deftest uniformized-markov-jump2 (continuous-time-tests)
  (let+ ((n 100000)
         (keys #(:a :b :c))
         (rates #(0.1 0.9 1.5))
         (total-rates (clnu:sum rates))
         (durations (make-array n :element-type 'double-float))
         (jumps (make-array n))
         ((&flet test-count (key rate)
            (let ((count (count key jumps))
                  (expected (* (/ rate total-rates) n)))
              (format *debug-io*
                      "key: ~A  expected count: ~A  actual count: ~A~%"
                      key (round expected) count)
              (assert-equality #'num= expected count))))
         (rv (r-uniformized-markov-jump rates :keys keys)))
    (dotimes (i n)
      (setf (values (aref durations i) (aref jumps i)) (draw rv)))
    (let ((*num=-tolerance* 0.02d0))
      (assert-equality #'num= (/ total-rates) (clnu:mean durations)))
    (let ((*num=-tolerance* 0.02d0))
      (map nil #'test-count keys rates))
    (assert-true (every #'alexandria:non-negative-real-p durations))
    (assert-true (every (lambda (j) (find j keys)) jumps))))
